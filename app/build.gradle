apply plugin: "com.android.application"
apply plugin: "androidx.navigation.safeargs"
apply plugin: "com.google.gms.google-services"
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    buildFeatures {
        viewBinding = true
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        //exclude protobuf-javalite cause of duplicate class
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    }
    defaultConfig {
        applicationId "com.picone.go4lunch"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "google_maps_key",
                (project.findProperty("GOOGLE_MAPS_API_KEY") ?: "")
        resValue "string", "facebook_key",
                (project.findProperty("FACEBOOK_API_ID") ?: "")
        resValue "string", "facebook_login_protocol_scheme",
                (project.findProperty("FACEBOOK_LOGIN_PROTOCOL_SHEME") ?: "")
        resConfigs "en", "fr", "es"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation Androidx.appcompat
    implementation Androidx.constraintLayout
    implementation Androidx.legacySupport
    //Test
    testImplementation Testing.jUnit
    androidTestImplementation Testing.jUnitAndroidx
    androidTestImplementation Testing.espresso
    //Core reference
    implementation project(':core')
    //RecyclerView
    implementation RecyclerViewDependency.recyclerView
    //Lifecycle
    implementation LifecycleDependency.viewModel
    implementation LifecycleDependency.liveData
    implementation LifecycleDependency.common
    implementation LifecycleDependency.viewModelSavedStates
    implementation LifecycleDependency.lifeCycle
    //Navigation Component
    implementation NavigationcomponentDependency.navigationUi
    implementation NavigationcomponentDependency.navigationFragment
    implementation NavigationcomponentDependency.dynamicFeatures
    androidTestImplementation NavigationcomponentDependency.navigationTesting
    //MaterialComponent
    implementation 'com.google.android.material:material:1.1.0'
    //Firebase
    implementation FirebaseDependency.core
    implementation FirebaseDependency.fireAuth
    implementation FirebaseDependency.fireStore
    //Google
    implementation GoogleauthDependency.googleAuth
    //Multidex
    implementation 'com.android.support:multidex:1.0.3'
    //Facebook
    implementation FacebookauthDependency.facebookSdk
    implementation FacebookauthDependency.facebookLogin
    //Google maps
    implementation GooglemapsDependency.maps
    implementation GooglemapsDependency.mapsLocation
    //Places
    implementation GooglemapsDependency.places
    //Lottie
    implementation "com.airbnb.android:lottie:3.0.0"
    //Hilt
    implementation HiltDependency.hilt
    annotationProcessor HiltDependency.hiltCompiler
    implementation HiltDependency.hiltViewModel
}
